table _gInteligenciaTemporal
	lineageTag: 66cf20b9-884a-445d-bab2-366382c9aa9b

	calculationGroup

		calculationItem Atual = SELECTEDMEASURE ( )

		calculationItem 'Ano Anterior' = ```
														
									CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        SAMEPERIODLASTYEAR ( dCalendario[Data] )
				                    )
									
				```

		calculationItem 'Atual Vs Ano Anterior' = ```
				
				
				VAR __Anterior = 
				    CALCULATE ( 
				        SELECTEDMEASURE ( ),
				        _gInteligenciaTemporal[Item] = "Ano Anterior"
				    )
				
				VAR __Result = 
				    DIVIDE (
				        SELECTEDMEASURE ( ) - __Anterior,
				        ABS ( __Anterior )
				    )
				
				RETURN __Result 
				        
				```

			formatStringDefinition = ```
					"0.0%;-0.0%" 
					```

		calculationItem 'Mes Anterior' = ```
									
									CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        PREVIOUSMONTH ( dCalendario[Data] )
				                    ) 
										
									
				```

		calculationItem 'Atual Vs Mes Anterior' = ```
				
				
				VAR __Anterior = 
				    CALCULATE ( 
				        SELECTEDMEASURE ( ),
				        _gInteligenciaTemporal[Item] = "Mes Anterior"
				    )
				
				VAR __Result = 
				    DIVIDE (
				        SELECTEDMEASURE ( ) - __Anterior,
				        ABS ( __Anterior )
				    )
				
				RETURN __Result 
				```

			formatStringDefinition = ```
					"0.0%;-0.0%" 
					```

		calculationItem 'Semana Anterior' = ```
									
									
									CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        OFFSET (
				                            -1,
				                            ALLSELECTED (
				                                dCalendario[SemanaAnoNomeISO], 
				                                dCalendario[SemanaAnoNumeroISO] 
				                            ),
				                            ORDERBY ( dCalendario[SemanaAnoNumeroISO], ASC )
				                        )
				                    ) 
									
									
				```

		calculationItem 'Atual Vs Semana Anterior' = ```
				
				
				VAR __Atual = SELECTEDMEASURE ( )
				VAR __Anterior = 
				    CALCULATE (
				        SELECTEDMEASURE ( ),
				        _gInteligenciaTemporal[Item] = "Semana Anterior"
				    ) 
				
				VAR __Resultado =
				    DIVIDE (
				        __Atual - __Anterior,
				        ABS ( __Anterior )
				    ) 
				
				RETURN __Resultado 
				```

			formatStringDefinition = "0.0%;-0.0%"

		calculationItem 'Dia Anterior' = ```
				
				    CALCULATE (
				        SELECTEDMEASURE ( ),
				        DATEADD ( 
				            dCalendario[Data],
				            -1,
				            DAY
				        )
				    ) 
				```

		calculationItem 'Atual Vs Dia Anterior' = ```
				
				
				VAR __Atual = SELECTEDMEASURE ( )
				VAR __Anterior = 
				    CALCULATE (
				        SELECTEDMEASURE ( ),
				        _gInteligenciaTemporal[Item] = "Dia Anterior"
				    ) 
				
				VAR __Resultado =
				    DIVIDE (
				        __Atual - __Anterior,
				        ABS ( __Anterior )
				    ) 
				
				RETURN __Resultado 
				```

			formatStringDefinition = ```
					"0.0%;-0.0%" 
					```

		calculationItem 'Trimestre Anterior' = ```
				
				                    CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        DATEADD ( 
				                            dCalendario[Data],
				                            -1,
				                            QUARTER
				                        )
				                    ) 
														
				```

		calculationItem 'Atual Vs Trimestre Anterior' = ```
				
				
				VAR __Atual = SELECTEDMEASURE ( )
				VAR __Anterior = 
				    CALCULATE (
				        SELECTEDMEASURE ( ),
				        _gInteligenciaTemporal[Item] = "Trimestre Anterior"
				    ) 
				
				VAR __Resultado =
				    DIVIDE (
				        __Atual - __Anterior,
				        ABS ( __Anterior )
				    ) 
				
				RETURN __Resultado 
				```

		calculationItem 'Media Movel Dias' = ```
									
				VAR __Qtd = 3 
				
				VAR __InicioRel = -1 * ( __Qtd - 1  )
				
				VAR __relacao = 
				    ALLSELECTED ( dCalendario[Data] )
				
				VAR __tabela =
				    WINDOW (
				        __InicioRel, REL,
				        0, REL,
				        __relacao,
				        ORDERBY ( dCalendario[Data] )
				    )
				
				VAR __Resultado = 
				    AVERAGEX ( 
				        __tabela,
				        SELECTEDMEASURE ( )  
				    )
				
				RETURN __Resultado 
				```

		calculationItem 'Media Movel Semanas' = ```
									
				VAR __Qtd = 3
				
				VAR __InicioRel = -1 * ( __Qtd - 1  )
				
				VAR __relacao = 
				    ALLSELECTED ( dCalendario[SemanaAnoNomeISO], dCalendario[SemanaAnoNumeroISO] )
				
				VAR __tabela =
				    WINDOW (
				        __InicioRel, REL,
				        0, REL,
				        __relacao,
				        ORDERBY ( dCalendario[SemanaAnoNumeroISO] )
				    )
				
				VAR __Resultado = 
				    AVERAGEX ( 
				        __tabela,
				        SELECTEDMEASURE ( )
				    )
				
				RETURN __Resultado 
				```

		calculationItem 'Media Movel Meses' = ```
				
				
				VAR __Qtd = 3
				
				VAR __InicioRel = -1 * ( __Qtd - 1  )
				
				VAR __relacao = 
				    ALLSELECTED ( dCalendario[MesAnoNome], dCalendario[MesAnoNumero] )
				
				VAR __tabela =
				    WINDOW (
				        __InicioRel, REL,
				        0, REL,
				        __relacao,
				        ORDERBY ( dCalendario[MesAnoNumero] )
				    )
				
				VAR __Resultado = 
				    AVERAGEX ( 
				        __tabela,
				        SELECTEDMEASURE ( ) 
				    )
				
				RETURN __Resultado 
				
				```

		calculationItem 'Media Movel Trimestres' = ```
				
				
				VAR __Qtd = 3
				
				VAR __InicioRel = -1 * ( __Qtd - 1  )
				
				VAR __relacao = 
				    ALLSELECTED ( dCalendario[TrimestreAnoNome], dCalendario[TrimestreAnoNumero] )
				
				VAR __tabela =
				    WINDOW (
				        __InicioRel, REL,
				        0, REL,
				        __relacao,
				        ORDERBY ( dCalendario[TrimestreAnoNumero] )
				    )
				
				VAR __Resultado = 
				    AVERAGEX ( 
				        __tabela,
				        SELECTEDMEASURE ( ) 
				    )
				
				RETURN __Resultado 
				```

		calculationItem 'Acumulado Anual' = ```
														
									CALCULATE ( 
				                        SELECTEDMEASURE ( ),
				                        DATESYTD ( dCalendario[Data] )
				                    )
									
				```

		calculationItem 'Acumulado Eterno' = ```
									
				VAR __DataContexto = MAX ( dCalendario[Data] )
				
				VAR __Resultado = 
				    CALCULATE (
				        SELECTEDMEASURE ( ),
				        dCalendario[Data] <= __DataContexto
				    )
				
				RETURN __Resultado 
				
				```

		calculationItem 'Acumulado Mensal' = ```
														
				                    CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        DATESMTD ( dCalendario[Data] )
				                    )
									
				```

		calculationItem 'Acumulado Trimestral' = ```
									
				                    CALCULATE (
				                        SELECTEDMEASURE ( ),
				                        DATESQTD ( dCalendario[Data] )
				                    )		
									    
				```

	column Item
		dataType: string
		lineageTag: 2a8d1773-0311-4613-94a2-9d8b8b43d6a8
		summarizeBy: none
		sourceColumn: Name
		sortByColumn: Ordinal

		annotation SummarizationSetBy = Automatic

	column Ordinal
		dataType: int64
		formatString: 0
		lineageTag: 1e2dd5e8-5e6a-4746-9375-0dc5e4e94f0d
		summarizeBy: sum
		sourceColumn: Ordinal

		annotation SummarizationSetBy = Automatic

